5 GOSUB 4000
7 GOSUB 5000
9 LET ROW = 0: LET COL = 0: GOSUB 1100

10 PRINT "PianoMatt's ADC Test Program"
11 PRINT "m-harrison.org"
12 PRINT "Choose base address:"
13 INPUT ADR
14 IF ADR < 256 THEN GOTO 30
15 GOTO 10

30 GOSUB 4000
35 LET ROW = 0: LET COL = 0: GOSUB 1100
40 FOR I=0 TO 7
50 POTCOL = 15:LET INK = POTCOL:GOSUB 1000
60 OUT I+ADR,0:POTVAL=INP(I+ADR)
70 IF POTVAL < 150 THEN POTCOL = 15
80 IF POTVAL >=150 AND POTVAL < 200 THEN POTCOL = 11
90 IF POTVAL >=200 THEN POTCOL = 1
100 PRINT "Input ";:PRINT I+1;:PRINT "= ";
105 LET INK = POTCOL : GOSUB 1000 : PRINT POTVAL
110 NEXT I
120 GOTO 35

999 END

1000 REM Ink Colour
1010 LET INK$ = STR$(INK)
1020 PRINT CHR$(27) + "[38;5;";RIGHT$(INK$, LEN(INK$) - 1);"m";
1030 RETURN

1050 REM Paper Colour
1060 PAPER$ = STR$(PAPER)
1070 PRINT CHR$(27) + "[48;5;" + RIGHT$(PAPER$, LEN(PAPER$) - 1) + "m";
1080 RETURN

1100 REM Move Row - Col
1110 LET ROW$ = STR$(ROW): LET COL$ = STR$(COL)
1120 PRINT CHR$(27) + "[";RIGHT$(ROW$, LEN(ROW$) - 1);";";
1130 PRINT RIGHT$(COL$, LEN(COL$) - 1);"H";
1140 RETURN

3000 REM Reset to default
3010 PRINT CHR$(27);CHR$(91);CHR$(48);CHR$(109)
3020 RETURN

4000 REM Clear screen
4010 PRINT CHR$(27)+"[2J"
4020 RETURN

5000 REM hide cursor
5010 PRINT CHR$(27)+"[?25l"
5020 RETURN

6000 REM Show cursor
6010 PRINT CHR$(27)+"[?25h"
6020 RETURN
